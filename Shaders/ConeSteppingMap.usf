const int SearchSteps = 128;
float BestConeRatio = 1;

float2 halfTexelSize = TexelSize.xy * 0.5;
//TODO: Uncomment the line below to get a visually smoother result whose correctness though is still to be verified
//halfTexelSize = 0; 

float currentTexelDepth = Texture2DSample(ReliefMap, ReliefMapSampler, UV).w;
//Find the best ratio from the batch of texels
for(int i = (int)StartTexelIndex; i < (int)EndTexelIndex; i++)
{
	int rowIndex = i / (int)TexelSize.z;
	int colomnIndex = i % (int)TexelSize.z;
	float2 lookUpUV = float2(colomnIndex * TexelSize.x + halfTexelSize.x, rowIndex * TexelSize.y + halfTexelSize.y);

	float3 p = float3(UV, 0);

	float3 o = float3(lookUpUV, 0);
	o.z = Texture2DSample(ReliefMap, ReliefMapSampler, o.xy).w;

	float3 v = o - p;
	v /= v.z;
	v *= 1.0 - o.z;
	v /= SearchSteps;

	p = o;

	//Keep stepping forward until we have just come out(the second intersection between the ray and the relief surface) of the relief surface
	for(int j = 0; j < SearchSteps; j++)
	{
		float d = Texture2DSample(ReliefMap, ReliefMapSampler, p.xy).w;

		if (d <= p.z)
			p += v;
	}

	float ratio = length(p.xy - UV);

	ratio = (p.z >= currentTexelDepth) ? 1.0 : ratio / (currentTexelDepth - p.z);		

	if (BestConeRatio > ratio)
	{
		BestConeRatio = ratio;
	}
}

//ConeSteppingMap stores the best ratio found in all previous draw calls so far, which will be compared with the best ratio found in the current draw call to get the latest best ratio
float ratio = Texture2DSample(ConeSteppingMap, ConeSteppingMapSampler, UV).x;
if(BestConeRatio > ratio)
{
	BestConeRatio = ratio;
}

return float4(BestConeRatio, currentTexelDepth, 0, 0);
